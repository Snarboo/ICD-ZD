ACTOR ICD_Knife : BaseWeapon
{
	Game Doom
	Tag "$ICDKNIFE"
	Weapon.SelectionOrder 1900
	//Weapon.AmmoGive2 1
	Weapon.AmmoType2 "ICD_KnifeAmmo"
	Inventory.Pickupmessage "$ICDKNIFEPICKUP"
	+WEAPON.NOALERT
	//+WEAPON.AMMO_OPTIONAL
	Weapon.UpSound "weapons/knife/upsound"
	Obituary "%k thought he was Riddick."
	Tag "Knife"
	States
	{
		Select:
			KNIF A 1 A_Raise
			Loop
		Deselect:
			KNIF A 1 A_Lower
			Loop
		Ready:
			KNIF A 1 A_WeaponReady
			Loop
		Fire:
			KNIF B 3
			KNIF C 2 A_PlaySound("weapons/knife/knifeswing")
			KNIF D 1 A_Saw(NULL,"weapons/knife/knifehit",random(3,9),"ICD_KnifeSpark",SF_NORANDOM,62,6,6)
			// Attacks deal damage differently in EDGE. They are based on a
			// range of values between X and Y, without being multiplied by a 
			// random number like how Doom's damage works. (Snarboo)
			TNT1 A 0 A_Jump(127,"Fire.Two")
			KNIF E 1 A_Saw(NULL,"weapons/knife/knifehit",random(3,9),"ICD_KnifeSpark",SF_NORANDOM,62,6,6) // - Edited by Snarboo
			KNIF F 1 A_Saw(NULL,"weapons/knife/knifehit",random(3,9),"ICD_KnifeSpark",SF_NORANDOM,62,6,6) // - Edited by Snarboo
			KNIF G 2
			KNIF H 2
			TNT1 A 3
			TNT1 A 0 A_Jump(127,"Fire.Rest")
			TNT1 A 3
		Fire.Two:
			KNIF H 2 A_PlaySound("weapons/knife/knifeswing2")
			KNIF I 2 A_Saw(NULL,NULL,random(5,20),"ICD_KnifeSpark2",SF_NORANDOM,62,6,6) // - Edited by Snarboo
			KNIF J 2 A_Saw(NULL,NULL,random(5,20),"ICD_KnifeSpark2",SF_NORANDOM,62,6,6) // - Edited by Snarboo
			KNIF K 3 A_Saw(NULL,NULL,random(5,20),"ICD_KnifeSpark2",SF_NORANDOM,62,6,6) // - Edited by Snarboo
		Fire.Rest:
			TNT1 A 6
			TNT1 A 5 A_ReFire
			THRW L 2
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_KnifeAmmo",1,1)
			Goto AltFire.NoAmmo
		//	TNT1 A 0 A_AlertMonsters - Knife throw alerted monsters in some version of EDGE (Snarboo)
			TNT1 A 0 A_TakeInventory("ICD_KnifeAmmo", 1)
			THRW L 2
			TNT1 A 3
			THRW EFGHI 1
			THRW A 4
			THRW J 2
			TNT1 A 5
			THRW J 1
			THRW K 1 A_FireCustomMissile("ICD_ThrownKnife",frandom(-2,2),1,4,0,0,5)
			THRW BCD 1
			TNT1 A 16
			THRW L 2
			Goto Ready
		AltFire.NoAmmo:
			KNIF A 1 A_WeaponReady(WRF_NOSECONDARY)
			Goto Ready
	}
}

ACTOR ICD_KnifeAmmo : Ammo
{
	Game Doom
	Scale 0.8
	Inventory.PickupMessage "$GotKnife"
	Inventory.Amount 1
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 2
	Ammo.BackpackMaxAmount 10
	Inventory.Icon KNFEB0 // - Changed by Snarboo
	States
	{
		Spawn:
			KNFE B -1
			Stop
	}
}

ACTOR ICD_ThrownKnife
{
	Radius 8
	Height 8
	Speed 25
	Damage (48)
	Projectile
	-NOGRAVITY
	+USEBOUNCESTATE
	+NOEXTREMEDEATH
	BounceType Doom
	BounceFactor 0.4
	BounceCount 4
	Scale 0.8
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("weapons/knife/knifeswing")
			KNFE FGHIJA 1
		Spawn.HoldA:
			KNFE A 1
			Loop
		Bounce.Floor:
		Bounce.Ceiling:
		Bounce.Wall:
			KNFE A 2 A_PlaySound("weapons/generic/smack")
			KNFE EAC 2
		Goto Spawn.HoldA
		XDeath:
			TNT1 A 0 A_PlaySound("weapons/knife/knifehit")
			TNT1 A 0 A_ChangeFlag(NOGRAVITY, 1)
			KNFE A 5
			TNT1 A 0 A_SpawnItem("ICD_KnifeAmmo")
		Stop
		Death:
			TNT1 A 0 A_PlaySound("weapons/generic/smack")
			TNT1 A 0 A_ChangeFlag(NOGRAVITY, 1)
			KNFE A 5
			TNT1 A 0 A_SpawnItem("ICD_KnifeAmmo")
			Stop
	}
}

ACTOR ICD_KnifeSpark
{
	Game Doom
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	RenderStyle Translucent
	Alpha 0.5
	Mass 5
	scale 0.6
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("weapons/knife/knifeslice")
			SMKE BCDEFGH 2
			stop
	}
}

ACTOR ICD_KnifeSpark2 : BulletPuff
{
	Game Doom
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+PUFFONACTORS
	+RANDOMIZE
	RenderStyle Translucent
	Alpha 0.5
	Mass 5
	scale 0.6
	States
	{
		Crash:
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("weapons/knife/knifeslice2")
			TNT1 A 0 A_SpawnItem("ICD_Puff_Spawn_2")
			SMKE BCDEFGH 2
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("weapons/knife/knifegore")
			stop
	}
}
