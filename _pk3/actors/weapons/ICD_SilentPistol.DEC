ACTOR ICD_SilentPistol : BaseWeapon
{
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoType "ICD_SPistolMagazine"
	Weapon.AmmoGive2 6
	Weapon.AmmoType2 "ICD_Bullets"
	Obituary "$OB_MPPISTOL"
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Tag "M1911-S"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Ready:
			SPIS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Deselect:
			SPIS A 1 A_Lower
			Loop
		Select:
			SPIS A 0 A_PlaySound("weapons/generic/holsterup")
			SPIS AAAAA 1 A_Raise
			SPIS A 0 A_PlaySound("weapons/1911/hammer")
			SPIS A 1 A_Raise
			Goto Select+7
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_SPistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			TNT1 A 0 A_PlaySound("weapons/1911-S/fire", CHAN_WEAPON)
			SPIS B 2 Bright A_FireBullets(1,1,1,5,"ICD_BulletPuff")
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,3.5,2.4,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			SPIS C 2
			SPIS H 2
			SPIS A Random(1, 2) // - Changed by Snarboo
			SPIS A 2 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_SPistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			SPIS A 2
			TNT1 A 0 A_PlaySound("weapons/1911-S/fire", CHAN_WEAPON)
			SPIS B 2 Bright A_FireBullets(2,2,1,5,"ICD_BulletPuff")
			TNT1 A 0 A_TakeInventory("ICD_SPistolMagazine",1)
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,random(1,3),2.4,0,frandom(-5,5))
			SPIS C 2 A_FireCustomMissile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			SPIS A 0 A_ReFire
			SPIS A 5
			Goto Ready
		Reload:
			SPIS A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_SPistolMagazine",12,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, 1)
			Goto Ready
			SPIS C 2
			SPIS D 1 A_PlaySound("weapons/1911/clipout")
			SPIS D 8 A_FireCustomMissile("ICD_EmptyPistolClipDrop",frandom(-6,6),0,2,-8,0,frandom(-3,3))
			SPIS F 6
		Reload.InfoPhase:
			TNT1 A 0 A_TakeInventory("ICD_Bullets",1)
			TNT1 A 0 A_GiveInventory("ICD_SPistolMagazine",1)
			TNT1 A 0 A_JumpIfInventory("ICD_SPistolMagazine",12,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.Restart")
			PISR G 3 A_PlaySound("weapons/1911/clipin")
			PISR H 3
			PISR L 4
			PISR I 4 A_PlaySound("weapons/1911/slide")
			PISR JK 2
			SPIS A 4
			Goto Ready
		Reload.Restart:
			TNT1 A 0
			Goto Reload.InfoPhase
		Spawn:
			SPIS A -1
			Stop
	}
}

ACTOR ICD_SPistolMagazine : Ammo
{
	Inventory.MaxAmount 12
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 12
	Inventory.Icon CLIPB0
	+INVENTORY.IGNORESKILL
}

/* Duplicate (Snarboo)
ACTOR ICD_EmptyPistolClipDrop
{
	Height 4
	Radius 4
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+MOVEWITHSECTOR
	+NOTELEPORT
	+FORCEXYBILLBOARD
	Speed 2
	Scale 0.6
	States
	{
		Spawn:
			CLP1 C 2
			Loop
		Death:
			TNT1 A 0 A_PlaySound("weapons/1911/clipdrop")
			CLP1 BEAFB 2
			CLP1 D 400
			CLP1 DDDD 105 A_FadeOut
			Stop
	}
}
*/