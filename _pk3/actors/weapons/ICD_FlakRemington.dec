ACTOR ICD_FlakRemington : BaseWeapon
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 0
	Weapon.AmmoType "ICD_FlakRemingtonMagazine"
	Weapon.AmmoType2 "ICD_FlakAmmo" // - Changed by Snarboo
	Inventory.PickupMessage "$GotRemington"
	Tag "Upgraded Combat Shotgun"
	Obituary "%k flakked %o."
	Weapon.UpSound "weapons/generic/weaponup"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("ICD_Remington")
			REMG A 1 A_Raise
			Loop
		Deselect:
			REMG A 1 A_Lower
			Loop
		Ready:
			REMG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_FlakRemingtonMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_FlakAmmo", 1, "Reload")
			Goto Ready
			TNT1 A 0 A_AlertMonsters
			REMG A 1 A_PlaySound("weapons/flakremington/fire", CHAN_WEAPON)
			REMG AAAA 0 A_FireCustomMissile("ICD_Gun3_Sparks", frandom(-5,5),0,5,0,0,frandom(-5,5))
			REMG AAA 0 A_FireCustomMissile("ICD_SSGSmoke1", frandom(-9,9),0,5,0,0,frandom(-5,5))
			REMG AAAAAAAA 0 A_FireCustomMissile("ICD_FLAK", frandom(-12,12),0,5,0,0,frandom(-5,5))
			REMG A 0 A_FireCustomMissile("ICD_FLAK", frandom(-12,12),1,5,0,0,frandom(-5,5))
			// Should now consume ammo! (Snarboo)
			REMG A 2
			REMG Q 2
			REMG A 3
			REMG C 2
			REMG D 3
			REMG E 3 A_PlaySound("weapons/flakremington/cock", CHAN_BODY)
			REMG F 4 A_FireCustomMissile("ICD_ShotgunShellSpawner1", 0, 0, 5, -4)  //Normal Shell -CC
			REMG E 4
			REMG D 4
			REMG C 3
			REMG A 1 A_ReFire
			Goto Ready
		AltFire: // -Crudux Cruo
		    TNT1 A 0 A_JumpIfInventory("ICD_FlakRemingtonMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_FlakAmmo", 1, "Reload")
			Goto Ready
		    TNT1 A 0 A_AlertMonsters
			REMG A 16 A_PlaySound("weapons/generic/click")
			REMG A 2 
			TNT1 A 0 A_PlaySound("weapons/flakremington/fire", CHAN_WEAPON)
			REMG J 5 A_PlaySound("weapons/40mm/gren2", CHAN_WEAPON,1.0,0,ATTN_NORM)    
            REMG A 0 A_FireCustomMissile("ICD_FlakBomb",(0),0,5,-2,0,4)
			REMG A 3 A_TakeInventory("ICD_FlakRemingtonMagazine",1)
		   	REMG C 2     
	        REMG D 3     
            REMG E 3 A_PLAYSOUND("weapons/flakremington/cock") 
            REMG F 4 A_FireCustomMissile("ICD_ShotgunShellSpawner2", 0, 0, 5, -4)  //New Shell -CC
            REMG E 4     
            REMG D 4     
            REMG C 3     
            REMG A 1 A_REFIRE 
            REMG A 0
	        Goto Ready
		   Reload: // added reloads -cc
			REMG A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_FlakRemingtonMagazine",8,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_FlakAmmo", 1, 1)
			Goto Ready
			// Reload should now be closer to the Flak Remington's in ICD (Snarboo)
			TNT1 A 0 A_PlaySound("weapons/generic/emptyclick2")
			REMG A 4
			REMG K 3
			REMG L 3
			MISF B 4
			REMG M 3
		Reload.Actual:
			REMG N Random(3,6) // Also not 100% accurate, but it's closer to how it was in EDGE! (Snarboo)
			SHTG O 3 A_PlaySound("weapons/doublebarrel/load", 6)
			TNT1 A 0 A_TakeInventory("ICD_FlakAmmo",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_FlakRemingtonMagazine",1)
			SHTG O 3 A_WeaponReady(WRF_NOBOB)
			REMG P 3 A_WeaponReady(WRF_NOBOB)
			SHTG P 3 A_WeaponReady(WRF_NOBOB)
			TNT1 A 0 A_JumpIfInventory("ICD_FlakRemingtonMagazine", 8, "Reload.Done")
			TNT1 A 0 A_JumpIfInventory("ICD_FlakAmmo", 1, "Reload.Actual")
		Reload.Done:
			REMG N 3	
			REMG M 3	
			MISF B 3	
			REMG L 3	
			REMG K 3	
			REMG C 3	
			REMG D 3	
			REMG E 4 A_PlaySound("weapons/Flakremington/cock", 6)
			REMG F 4
			REMG E 4
			REMG C 4
			Goto Ready
		Flash:
			SHTF B 1 bright	A_Light(1)
			SHTF A 2 bright A_Light(2)
			TNT1 A 0 A_Light(0)
			Goto LightDone
		Spawn: // - Added by Snarboo
			FLMD A -1
			Stop
	}
}

ACTOR ICD_FLAK
{
	Height 8
	Radius 8
	Speed 60
	Mass 2
	Damage (20)
	+MISSILE
	+MOVEWITHSECTOR
	//+BOUNCEONACTORS
	+NOTELEPORT
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	BounceSound "weapons/flak/bounce"
	renderstyle add
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("NULL")
			FRAG A 2 BRIGHT A_Explode(30,32,XF_HURTSOURCE)
			FRAG A 2 BRIGHT A_SpawnItem("ICD_FlakSmoke")
			FRAG A 2 BRIGHT
			Goto Spawn+2
		 Death:
			stop
	}
}

ACTOR ICD_FlakBomb // - CC
{
	Args 0, 0, 0, 0, 75
	Radius 12
	Height 8
	Speed 25
	Damage 5
	Scale 0.8
	PROJECTILE
	-NOGRAVITY
	States
	{
		Spawn:
			GRNL A 1 A_SpawnItemEx("ICD_RPUFF",0,0,-6)
			TNT1 A 0 A_CountdownArg(4, "XDeath")
			Loop
		Death:
			TNT1 A 0 A_ChangeFlag(NOGRAVITY, 1)
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SpawnItemEx("ICD_GrenadeThrownExplosion")
			TNT1 A 0 A_Explode(150)
			TNT1 A 0 A_PlaySound("weapons/boom")
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 0 A_SetAngle(angle+45)
			TNT1 A 0 A_CustomMissile("ICD_Fragment", 26, 0, random(-1,1), CMF_AIMDIRECTION | CMF_TRACKOWNER, 10)
			TNT1 A 6 
			TNT1 A 0 A_SpawnItemEx("ICD_Explosion_Smoke")
			Stop
	}
}

ACTOR ICD_FlakRemingtonMagazine : Ammo
{
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8
	Inventory.Icon FLMDB0 // - Changed by Snarboo
	+INVENTORY.IGNORESKILL
}

