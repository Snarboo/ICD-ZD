ACTOR ICD_Beretta : BaseWeapon
// Some changes made by Crudux Cruo!
{
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoType "ICD_BerettaMagazine"
	Weapon.AmmoType2 "ICD_Bullets"
	Obituary "%o tasted %k's Beretta!"
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Tag "9mm Automatic Pistol"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Select:
			PIST A 0 A_PlaySound("weapons/generic/holsterup", CHAN_BODY)
			BERT AAAAAA 1 A_Raise
			TNT1 A 0 A_PlaySound("weapons/1911/hammer", CHAN_WEAPON)
			BERT A 1 A_Raise
			Wait
		Deselect:
			BERT A 1 A_Lower
			Loop
		Ready:
			BERT A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.Actual")
			Goto Ready
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("weapons/beretta/fire", CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("ICD_BerettaMagazine", 1)
			BERT B 2 A_FireBullets(0.5,2,1,4,"ICD_BulletPuff")
			BERT C 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,0,2.4,0,frandom(-5,5))
			BERT C 1 A_FireCustomMissile("ICD_BerettaCasingSpawner",0,0,0,-2)
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.Actual")
			BERT G 2
			BERT F 2
			BERT D Random(0,1) // Closer to EDGE timings (Snarboo)
			BERT D 5 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",1,1)
			Goto Reload
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/beretta/fire", CHAN_WEAPON)
			BERT D 1 A_GunFlash("AltFlash")
			TNT1 A 0 A_TakeInventory("ICD_BerettaMagazine", 1)
			BERF A 1 BRIGHT A_FireBullets(0.5,2,-1,4,"ICD_BulletPuff")
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,0,2.4,0,frandom(-5,5))
			BERT E 1 A_FireCustomMissile("ICD_BerettaCasingSpawner",0,0,0,-2)
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",1,1)
			Goto Reload
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/beretta/fire", CHAN_WEAPON)
			BERT D 1 A_GunFlash("AltFlash")
			TNT1 A 0 A_TakeInventory("ICD_BerettaMagazine", 1)
			BERF A 1 BRIGHT A_FireBullets(0.5,2,-1,4,"ICD_BulletPuff")
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,0,2.4,0,frandom(-5,5))
			BERT E 1 A_FireCustomMissile("ICD_BerettaCasingSpawner",0,0,0,-2)
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",1,1)
			Goto Reload
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/beretta/fire", CHAN_WEAPON)
			BERT D 1 A_GunFlash("AltFlash")
			TNT1 A 0 A_TakeInventory("ICD_BerettaMagazine", 1)
			BERF A 1 BRIGHT A_FireBullets(0.5,2,-1,4,"ICD_BulletPuff")
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,0,2.4,0,frandom(-5,5))
			BERT E 1 A_FireCustomMissile("ICD_BerettaCasingSpawner",0,0,0,-2)
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, "Reload.Actual")
			BERT C 2
			BERT G 1
			BERT F 2
			BERT D 5
			TNT1 A 0 A_Refire
			Goto Ready
		Reload:
			BERT A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",15,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, "Reload.Actual")
			Goto Ready
		Reload.Actual:
			BERT C 5
			BERT H 2
			BERT J 2 A_PlaySound("weapons/1911/clipout", CHAN_BODY)
			BERT J 12 A_FireCustomMissile("ICD_EmptyBerettaClipDrop",frandom(-6,0),0,0,-8,0,frandom(-3,3))
			BERT L 3 A_PlaySound("weapons/1911/clipin", CHAN_BODY)
		Reload.Infophase:
			TNT1 A 0 A_TakeInventory("ICD_Bullets",1)
			TNT1 A 0 A_GiveInventory("ICD_BerettaMagazine",1)
			TNT1 A 0 A_JumpIfInventory("ICD_BerettaMagazine",15,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.InfoPhase")
			BERT H 6
			BERT K 7
			BERT G 7 A_PlaySound("weapons/beretta/slide", CHAN_BODY)
			BERT F 5
			BERT B 3
			Goto Ready
		AltFlash:
			TNT1 A 1
			TNT1 A 3 A_Light1
			TNT1 A 3 A_Light2
			TNT1 A 3 A_Light1
			TNT1 A 0 A_Light0
			Stop
		Flash:
			BERF A 2 Bright A_Light1
			TNT1 A 0 A_Light0
			Stop
		Spawn: // - Added by Snarboo
			HGUN B -1
			Stop
	}
}

ACTOR ICD_BerettaMagazine : Ammo
{
	Inventory.MaxAmount 15
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 15
	Inventory.Icon HGUNB0 // - Changed by Snarboo
	+INVENTORY.IGNORESKILL
}

ACTOR ICD_EmptyBerettaClipDrop
{
	Height 4
	Radius 4
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+MOVEWITHSECTOR
	+NOTELEPORT
	+FORCEXYBILLBOARD
	Speed 2
	Scale 0.6
	States
	{
		Spawn:
			CLP1 C 2
			Loop
		Death:
			TNT1 A 0 A_PlaySound("weapons/1911/clipdrop")
			CLP1 BEAFB 2
			CLP1 D 400
			CLP1 DDDD 105 A_FadeOut(0.25)
			Stop
	}
}